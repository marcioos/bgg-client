apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.marcioos'
archivesBaseName = "bgg-client"
version = '1.0'

description = "Java and Android client for BoardGameGeek XML API 2"

sourceCompatibility = 1.7
targetCompatibility = 1.7

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.8.1'
    compile(group: 'com.squareup.retrofit2', name: 'converter-simplexml', version: '2.8.1') {
        exclude(module: 'stax-api')
        exclude(module: 'stax')
        exclude(module: 'xpp3')
    }
    testCompile group: 'junit', name: 'junit', version: '4.13'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name "bgg-client"
        packaging 'jar'
        artifactId "bgg-client"
        description 'Java and Android client for BoardGameGeek XML API 2'
        url 'https://github.com/marcioos/bgg-client'

        scm {
          connection 'scm:git:git@github.com:marcioos/bgg-client.git'
          developerConnection 'scm:git:git@github.com:marcioos/bgg-client.git'
          url 'https://github.com/marcioos/bgg-client'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'marcioos'
            name 'Marcio Oliveira'
            email 'marciottlg@gmail.com'
          }
        }
      }
    }
  }
}

